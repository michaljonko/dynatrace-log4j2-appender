import java.nio.charset.Charset
import java.nio.file.Files
import java.text.SimpleDateFormat

plugins {
	id 'java'
	id 'maven-publish'
	id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
	id 'signing'
}

wrapper {
	gradleVersion = '8.14.3'
	distributionType = Wrapper.DistributionType.BIN
}

def appGroup = 'io.github.michaljonko'
def appName = 'dynatrace-log4j2-appender'
def appVersion = Files.readAllLines(rootDir.toPath().resolve('version.txt'), Charset.forName('UTF-8'))
		.first()
		.trim()
def appDesc = 'Log4j2 Appender for Dynatrace Generic Log Ingest'

defaultTasks 'clean', 'build', 'test'

group = appGroup
version = appVersion
description = appDesc

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
    withJavadocJar()
}

repositories {
	mavenCentral()
}

jar {
	manifest {
		attributes(
				'Built-By': 'Michal Jonko',
				'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
				'Created-By': "Gradle ${gradle.gradleVersion}",
				'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
				'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
		)
	}
}

ext["signing.keyId"] = project.findProperty('signing.keyId') ?: System.getenv("GPG_KEY_ID")
ext["signing.password"] = project.findProperty('signing.password') ?: System.getenv("GPG_PASSWORD")
ext["signing.secretKeyRingFile"] = project.findProperty('signing.secretKeyRingFile') ?: System.getenv("GPG_KEYRING_FILE")

signing {
	sign publishing.publications
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			groupId = appGroup
			artifactId = appName
			version = appVersion

			pom {
				name = appName
				description = appDesc
				url = 'https://michaljonko.github.io/dynatrace-log4j2-appender/'
				licenses {
					license {
						name = 'Apache-2.0'
						url = 'https://raw.githubusercontent.com/michaljonko/dynatrace-log4j2-appender/master/LICENSE'
					}
				}
				developers {
					developer {
						id = 'michaljonko'
						name = 'Michal Jonko'
						email = 'michal.jonko@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:github.com/michaljonko/dynatrace-log4j2-appender.git'
					developerConnection = 'scm:git:ssh://github.com/michaljonko/dynatrace-log4j2-appender.git'
					url = 'https://github.com/michaljonko/dynatrace-log4j2-appender'
				}
			}
		}
	}
}

nexusPublishing {
	repositories {
		sonatype {
			nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
			snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
			username = project.findProperty('sonatypeUsername') ?: System.getenv("SONATYPE_USERNAME")
			password = project.findProperty('sonatypePassword') ?: System.getenv("SONATYPE_TOKEN")
		}
	}
}

def log4jVersion = '2.25.3'
def junitVersion = '5.14.1'
def junitPlatformVersion = '1.14.1'
def mockitoVersion = '5.20.0'
def assertjVersion = '3.27.6'
def wireMockVersion = '3.13.2'
def awaitilityVersion = '4.3.0'
def lombokVersion = '1.18.42'

dependencies {
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
	annotationProcessor group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion

	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitVersion
	testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
	testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion
	testImplementation group: 'org.assertj', name: 'assertj-core', version: assertjVersion
	testImplementation group: 'org.wiremock', name: 'wiremock', version: wireMockVersion
	testImplementation group: 'org.awaitility', name: 'awaitility', version: awaitilityVersion
	testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

test {
	useJUnitPlatform()
}