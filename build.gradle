import java.nio.charset.Charset
import java.nio.file.Files
import java.text.SimpleDateFormat

plugins {
	id 'java'

	id 'maven-publish'
	id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
	id 'signing'
}

wrapper {
	gradleVersion = '8.14.3'
	distributionType = Wrapper.DistributionType.BIN
}

def appGroup = 'io.github.michaljonko'
def appName = 'dynatrace-log4j2-appender'
def appVersion = Files.readAllLines(rootDir.toPath().resolve('version.txt'), Charset.forName('UTF-8'))
		.first()
		.trim()
def appDesc = 'Log4j2 Appender for Dynatrace Generic Log Ingest'

defaultTasks 'clean', 'build', 'test'

group = appGroup
version = appVersion
description = appDesc

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
	mavenCentral()
}

jar {
	manifest {
		attributes(
				'Built-By': 'Michal Jonko',
				'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
				'Created-By': "Gradle ${gradle.gradleVersion}",
				'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
				'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
		)
	}
}

signing {
	sign publishing.publications
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			groupId appGroup
			artifactId appName
			version appVersion

			pom {
				name = appName
				description = appDesc
				url = 'https://michaljonko.github.io/dynatrace-log4j2-appender/'
				licenses {
					license {
						name = 'Apache-2.0'
						url = 'https://raw.githubusercontent.com/michaljonko/dynatrace-log4j2-appender/master/LICENSE'
					}
				}
				developers {
					developer {
						id = 'michaljonko'
						name = 'Michal Jonko'
						email = 'michal.jonko@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:github.com/michaljonko/dynatrace-log4j2-appender.git'
					developerConnection = 'scm:git:ssh://github.com/michaljonko/dynatrace-log4j2-appender.git'
					url = 'https://github.com/michaljonko/dynatrace-log4j2-appender'
				}
			}
		}
	}
}

nexusPublishing {
	repositories {
		sonatype {
			nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
			snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
			username = project.findProperty('sonatypeUsername') ?: System.getenv("SONATYPE_USER")
			password = project.findProperty('sonatypePassword') ?: System.getenv("SONATYPE_PASSWORD")
			stagingProfileId = project.findProperty('sonatypeStagingProfileId') ?: System.getenv("SONATYPE_STAGING_PROFILE_ID")
		}
	}
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.25.2'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.25.2'
	annotationProcessor group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.25.2'

	implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'

	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.14.1'
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.20.0'
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.20.0'
	testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.6'
	testImplementation group: 'org.wiremock', name: 'wiremock', version: '3.13.2'
	testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.3.0'
	testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.42'

	testImplementation 'org.junit.platform:junit-platform-launcher:1.14.1'
}

test {
	useJUnitPlatform()
}