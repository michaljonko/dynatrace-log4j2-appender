import java.text.SimpleDateFormat

plugins {
	id 'java'
	id 'com.zoltu.application-agent' version '1.0.14'
}

wrapper {
	gradleVersion = '8.14.3'
	distributionType = Wrapper.DistributionType.BIN
}

group 'pl.coffeepower'
version '1.0-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

repositories {
	mavenLocal()
	mavenCentral()
}

application {
	mainClassName = 'io.github.michaljonko.log4j.example.SimpleLogsGeneratorApp'
}

applicationAgent {
	applyToTests false
	applyToRun true
	applyToStartScripts true
}

jar {
	manifest {
		attributes(
				'Built-By': 'Michal Jonko',
				'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
				'Created-By': "Gradle ${gradle.gradleVersion}",
				'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
				'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
		)
	}
}

dependencies {
	compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.42'

	compileOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.25.3'
	runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.25.3'
	runtimeOnly group: 'io.github.michaljonko', name: 'dynatrace-log4j2-appender', version: '0.0.+'

	agent group: 'io.opentelemetry.javaagent', name: 'opentelemetry-javaagent', version: '1.33.6'
}
